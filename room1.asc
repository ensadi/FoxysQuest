// room script file

function region1_WalksOnto()
{
  cFoxyMonk.ChangeRoom(2, 155, 210);
}

function hTree_Look()
{
  	Display("That's a big tree next to the pond.");
}

function oStick_Interact()
{
  cFoxyMonk.Walk(oStick.X, oStick.Y, eBlock, eWalkableAreas);
  oStick.Visible = false;
  cFoxyMonk.AddInventory(iStick);
  Display("You stick the stick in your pocket and hope it's not a sticky stick that will stick you.");
}

function oApple_UseInv()
{
  if (cFoxyMonk.ActiveInventory == iStick) 
  {
    if (Game.DoOnceOnly("StickIsInTree") == true) 
    {
      // Have Foxy walk to a prime throwing position
      cFoxyMonk.Walk(116, 164, eBlock, eWalkableAreas);
      
      // Turn Foxy to face the apple
      cFoxyMonk.FaceObject(oApple);
      
      // Since she's going to throw the stick, it needs to disappear from her inventory.
      cFoxyMonk.LoseInventory(iStick);
      
      // Change her view to the new throwing view and lock it
      cFoxyMonk.LockView(VFMTHROW);
      // Animate the new view
      cFoxyMonk.Animate(0, 5, eOnce, eBlock, eForwards);
      // Unlock the view
      cFoxyMonk.UnlockView();
      // Lock frame 2 of the view so her arm stays up in throwing position
      cFoxyMonk.LockViewFrame(VFMTHROW, 0, 2);
      
      // Set the x and y location of the stick object to be wherever Foxy is, and then make it visible.
      oStick.X = cFoxyMonk.x;
      oStick.Y = cFoxyMonk.y - 10;
      oStick.Visible = true;

      // Override the baseline of the stick so it doesn't appear behind the tree.
      oStick.Baseline = 200;
      
      // Move the stick to where the apple is.
      oStick.Move(oApple.X, oApple.Y, 6, eBlock, eAnywhere);
      
      // Now move the stick down to the ground with the apple.  Notice the use of eNoBlock here, since we want the apple to fall at the same time.
      oStick.Move(oApple.X, 160, 5, eNoBlock, eAnywhere);
      oApple.Move(oApple.X, 170, 4, eBlock, eAnywhere);

      // Reset the baselines for both the apple and the stick back to the default.
      oApple.Baseline = 0;
      oStick.Baseline = 0;
      
      // Unlock the view
      cFoxyMonk.UnlockView();
      
      Display("Good shot! The apple falls out of the tree and lands on the ground with a thud.");
      dDialog0.SetOptionState(3, eOptionOffForever);
    }
  }
}

function oApple_Interact()
{
  if (oApple.Y == 170) 
  {
    cFoxyMonk.Walk(oApple.X, oApple.Y, eBlock, eWalkableAreas);
    oApple.Visible = false;
    cFoxyMonk.AddInventory(iApple);
    Display("Foxy puts the apple away. Maybe she can eat it as a snack later.");
  }
  else 
  {
    Display("The apple is too high for Foxy to reach it!");
  }

}

function room_RepExec()
{
  if(Region.GetAtRoomXY(cDork.x, cDork.y) == region[2])
  {
    if(cDork.View != VDSWIM)
      cDork.ChangeView(VDSWIM);
  }
  else if(cDork.View != VDNORMAL)
  {
      cDork.ChangeView(4);
  }
}
